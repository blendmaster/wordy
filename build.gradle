group = "wordy"
version = "0.1"

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'

mainClassName = 'w.rdy.Wordy'

repositories {
  mavenCentral()
}

dependencies {
  compile 'com.fasterxml.jackson.core:jackson-databind:2.2.2'

  compile 'oauth.signpost:signpost-commonshttp4:1.2.1.2'

  testCompile 'junit:junit:4.11'
  testCompile 'co.freeside:betamax:1.1.2'
  // XXX betamax depends on groovy, but not through maven, apparently.
  testCompile 'org.codehaus.groovy:groovy-all:1.8.8'
}

test {
  // XXX java 8: http://issues.gradle.org//browse/GRADLE-2846
  scanForTestClasses = false
  include 'w/rdy/**'

  testLogging.showStandardStreams = true
}

jar {
  from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
  manifest { attributes 'Main-Class': mainClassName }
}

task make(type: Exec, dependsOn: 'build') {
  description = 'create shell executable (with appended jar)'
  inputs.dir jar.destinationDir
  outputs.file file('wordy')
  commandLine './build.sh'
}
